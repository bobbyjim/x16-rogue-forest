10 REM 
20 REM  INITIALIZE DESCRIPTORS
30 REM 
40 DIM M$(31)
50 FOR X=0 TO 31 :READ M$(X) :NEXT
60 DATA  DOPPLEGANGER,  ABALONE,  BASILISK,    CENTAUR,    DRAGON
70 DATA  ETTIN,         FUZZY,    GHOUL,       HELLHOUND,  IMP
80 DATA  JINN,          KOBOLD,   LEPRECHAUN,  MINOTAUR,   NYMPH
90 DATA  ORC,           PYTHON,   QUAGGA,      RATTLER,    SKELETON
100 DATA  TROLL,         UR-VILE,  VAMPIRE,     WRAITH,     XEROC
110 DATA  YETI,          ZOMBIE,   WYVERN,      MANTICORE,  VELOCIRAPTOR
120 DATA  SPHINX,        SPIDER
130 FOR X=0 TO 7 :READ N$(X) :NEXT
140 DATA  DURIN, FOLGAR, SARA, SHARIK, ZOG, CHENEY, REJNALDI, KHAALO
150 FOR X=0 TO 7 :READ C$(X) :NEXT
160 DATA  "",RED,GREEN,BLUE,YELLOW,WHITE,SILVER,GOLDEN
170 FOR X=0 TO 7 :READ D$(X) :NEXT
180 DATA  "",OF WAR,OF AGILITY,OF ENDURING,OF CLEVERNESS,OF SKILL,"",OF MAGIC
190 FOR X=0 TO 7 :READ W$(X) :NEXT
200 DATA DAGGER, QUARTERSTAFF, HAMMER, AXE, SWORD, GREAT AXE, CLAYMORE, HALBERD
210 FOR X=0 TO 7 :READ A$(X) :NEXT
220 DATA LEATHER, STUDDED LEATHER, HAUBERK, RINGMAIL, SCALE MAIL, PLATE MAIL
230 DATA ENCHANTED MAIL, MITHRIL
240 REM --------------
250 REM  LINE-CLEANER
260 REM --------------
270 LI$ = "                                                           "
280 REM ------------------------
290 REM  SET UP VIDEO REGISTERS
300 REM ------------------------
310 POKE $9F2D, %01100000 :REM MAP HEIGHT=1, WIDTH=2 = 64X128 TILES
320 POKE $9F2E, %10000000 :REM MAP BASE ADDR = 128 X512 = $10000.
330 POKE $9F2F, %01111100 :REM TILE BASE ADDR = 31 X2K  = $F800.
340 REM ALSO, TILE HT=0, WD=0, SO 8 PIXELS X 8 PIXELS
350 REM 
360 REM  NO SCROLLING, PLEASE ($9F30-$9F33)
370 REM 
380 POKE $9F30, 0
390 POKE $9F31, 0
400 POKE $9F32, 0
410 POKE $9F33, 0
420 ? CHR$(147) "WELCOME TO ROGUE FOREST"
430 INPUT "PRESS <ENTER> TO BEGIN"; K$
440 X=RND(-TI) :REM INIT
450 REM ----------------------------------
460 REM   BUILD FOREST
470 REM   ROUGHLY 1/3 TREES, 2/3 CLEARING
480 REM ----------------------------------
490 COLOR 1,0 :CLS  :REM 0=TRANSPARENT
500 FO(0) = $41 :REM TREE
510 FO(1) = $2E :REM DOT
520 FO(2) = $2E :REM DOT
530 FO(3) = $43 :REM HORIZ LINE
540 FO(4) = $42 :REM VERT LINE
550 FO(5) = $25 :REM TREASURE
560 FO(6) = $2A :REM AMULET OF YENDOR
570 POKE $9F29, %00110001 :REM $31=LAYERS 1,0. OUTPUT MODE=1 (VGA)
580 PRINT "\X13\X11\X11\X11\X11COLOR 1,6\X13" 
590 CC=0
600 REM ----------------------------------------------------------
610 REM  WE CAN SPEED UP THIS ROUTINE BY UNROLLING THE LOOP A BIT
620 REM  ONE AT A TIME = 17 SEC
630 REM  TWO AT A TIME = 13 SEC
640 REM  THREE AT A TIME = 12.5 SEC
650 REM  FOUR AT A TIME = 12 SEC
660 REM ----------------------------------------------------------
670 CL=$05  :REM BG/FG COLOR NYBBLES ($15=WHITE UNDER GREEN)
680 MN=0    :REM MONSTER NUMBER (0-31)
690 DIM MA(31) :REM MONSTER ADDRESS
700 DIM XM(31),YM(31) :REM MONSTER X,Y
710 DIM SO(31) :REM WHAT THE MONSTER IS STANDING ON
720 FOR Y=0 TO 45
730    Y0=Y*$100
740    FOR X=0 TO 158 STEP 8
750       P=Y0+X
760       IF Y<5 OR Y>40 GOTO 970
770       IF X<4 OR X>150 GOTO 970
780          FF=3 
790          IF Y>5 AND Y<40 THEN FF=INT(RND(1)*3) 
800          VPOKE 1,P,FO(FF)       :REM CHARACTER INDEX
810          VPOKE 1,P+1,CL        :REM BG/FG COLOR NYBBLES
820          IF Y>5 AND Y<40 THEN FF=INT(RND(1)*3) 
830          VPOKE 1,P+2,FO(FF)
840          VPOKE 1,P+3,CL              
850          IF Y>5 AND Y<40 THEN FF=INT(RND(1)*3) 
860          VPOKE 1,P+4,FO(FF)
870          VPOKE 1,P+5,CL              
880          IF Y>5 AND Y<40 THEN FF=INT(RND(1)*3) 
890          VPOKE 1,P+6,FO(FF)
900          VPOKE 1,P+7,CL              
910          IF Y<6 OR Y>39 GOTO 1050
920          REM TREASURE AND/OR CRITTER
930          IF RND(1) > 0.06 GOTO 1050
940             XX = INT(RND(4))
950             VPOKE 1,P+2*XX,$25 
960             GOTO 1050
970             VPOKE 1,P,$20
980             VPOKE 1,P+1,CL
990             VPOKE 1,P+2,$20
1000             VPOKE 1,P+3,CL
1010             VPOKE 1,P+4,$20
1020             VPOKE 1,P+5,CL
1030             VPOKE 1,P+6,$20
1040             VPOKE 1,P+7,CL
1050    NEXT
1060 NEXT
1070 REM
1080 REM PLACE MONSTERS
1090 REM
1100 FOR M = 0 TO 31
1110    X = 10 + INT(RND(1)*60)
1120    Y = 10 + INT(RND(1)*20)
1130    MA = Y * $100 + X * 2
1140    MA(M) = MA
1150    XM(M)=X
1160    YM(M)=Y
1170    SO(M) = VPEEK(1,MA)
1180    VPOKE 1,MA,M
1190 NEXT
1200 REM DRAW THE AMULET OF YENDOR
1210 VPOKE 1, INT(RND(1)*25+10) * $100 + INT(RND(1)*50+20) * 2, $2A
1220 REM
1230 REM  HIDE BACKGROUND
1240 REM
1250 COLOR 1,6 :CLS :REM WHITE ON BLUE
1260 REM
1270 REM  INITIALIZE DRAWING UTIL
1280 REM
1290 DIM VP$(60)
1300 VP$(0)="\X13"
1310 FOR V=1 TO 60
1320    VP$(V) = VP$(V-1) + "\X11"
1330 NEXT
1340 REM -------------------
1350 REM  INITIALIZE PLAYER
1360 REM -------------------
1370 X=10 :Y=10 :REM POSITION
1380 IW=0       :REM WEAPON INVENTORY
1390 IA=0       :REM ARMOR INVENTORY
1400 IT=0       :REM TREASURE INVENTORY
1410 PS=INT(RND(1)*50+50)  :REM PLAYER STRENGTH
1420 PD=INT(RND(1)*50+50)  :REM PLAYER DEXTERITY
1430 PE=INT(RND(1)*50+50)  :REM PLAYER ENDURANCE
1440 PI=INT(RND(1)*50+50)  :REM PLAYER INTELLIGENCE
1450 REM --------------------------------
1460 REM   CLEAR 9X9 GRID AROUND PLAYER
1470 REM --------------------------------
1480 ? VP$(Y-4); :REM LEFT$(PY$,Y);
1490 COLOR 1,0 :REM TRANSPARENT (CLEARS FOG)
1500 FOR I=1 TO 9
1510    ? TAB(X-4) "         "
1520 NEXT
1530 REM
1540 REM   PRINT PLAYER
1550 REM
1560 COLOR 1,6
1570 ? "\X91\X91\X91\X91\X91" TAB(X) "@"
1580 REM
1590 REM   POSITION CURSOR FOR EVENTS
1600 REM
1610 ? VP$(46);
1620 REM
1630 REM   PLAYER MOVEMENT
1640 REM
1650 GET A$ :IF A$="" GOTO 1650
1660 XX=0:YY=0
1670 IF A$="\X11" THEN YY=+1 :REM DOWN
1680 IF A$="\X91" THEN YY=-1 :REM UP
1690 IF A$="\X1D" THEN XX=+1 :REM RIGHT
1700 IF A$="\X9D" THEN XX=-1 :REM LEFT
1710 LO = VPEEK(1,(Y+YY)*$100 + (X+XX)*2)
1720 IF LO < $20 THEN NN=LO :GOSUB 2440 :GOTO 1760
1730 IF LO < $41 THEN X=X+XX :Y=Y+YY
1740 IF LO = $25 THEN GOSUB 1870
1750 IF LO = $2A THEN GOSUB 2060
1760 FOR NN=0 TO 31
1770    AA=MA(NN)
1780    XX=XM(NN)-X
1790    YY=YM(NN)-Y
1800    IF ABS(XX) < 2 AND ABS(YY) < 2 THEN GOSUB 2350 :GOTO 1830
1810    IF RND(1)>0.5 THEN 1830
1820    IF ABS(XX) < 9 AND ABS(YY) < 9 THEN GOSUB 2180
1830 NEXT
1840 IF (X<70) AND (X>3) AND (Y<40) AND (Y>5) GOTO 1480
1850 COLOR 1,$06 :REM WHITE ON BLUE
1860 END
1870 COLOR 1,6
1880 VPOKE 1,Y*$100 + X*2, $2E  :REM CLEAR IT
1890 T1=INT(RND(1)*8)
1900 T2=INT(RND(1)*8)
1910 T3=INT(RND(1)*8)
1920 T4=INT(RND(1)*8)
1930 TT=INT(RND(1)*2)
1940 IF TT=0 GOTO 1960
1950 IF TT=1 GOTO 2010
1960 ? "YOU FOUND " N$(T1) "'S " C$(T2) " " W$(T3) " " D$(T4)
1970 INPUT "DO YOU KEEP IT"; YN$
1980 ? "\X91\X91" LI$ :? LI$
1990 IF YN$ = "Y" THEN IW = T1 + T2 * 8 + T3 * 64 + T4 * 256
2000 RETURN
2010 ? "YOU FOUND " N$(T1) "'S " C$(T2) " " A$(T3) " " D$(T4)
2020 INPUT "DO YOU KEEP IT"; YN$
2030 ? "\X91\X91" LI$ :? LI$
2040 IF YN$ = "Y" THEN IA = T1 + T2 * 8 + T3 * 64 + T4 * 256
2050 RETURN
2060 COLOR 1,6
2070 ? "YOU FOUND THE AMULET OF YENDOR! "
2080 INPUT "PRESS <ENTER>"; YN$
2090 ? "\X91\X91" LI$ :? LI$
2100 VPOKE 1,Y*$100 + X*2, $2E  :REM CLEAR IT
2110 PY=1  :REM PLAYER-YENDOR
2120 RETURN
2130 REM
2140 REM  MOVE MONSTER
2150 REM  - NN IS THE MONSTER NUMBER (0-31)
2160 REM  - ADDRESS IN AA
2170 REM  - RELATIVE POS IN XX,YY
2180 VPOKE 1,AA,SO(NN)  :REM REDRAW TILE
2190 DX=0 :DY=0
2200 IF XX<0 THEN AA=AA+2 :DX=1 :GOTO 2240
2210 IF XX>0 THEN AA=AA-2 :DX=-1 :GOTO 2240
2220 IF YY<0 THEN AA=AA+$100 :DY=1 :GOTO 2240
2230 IF YY>0 THEN AA=AA-$100 :DY=-1 :GOTO 2240
2240 V=VPEEK(1,AA)
2250 IF V < $20 OR V = $2A GOTO 2310
2260 SO(NN)=VPEEK(1,AA)
2270 MA(NN)=AA
2280 XM(NN)=XM(NN)+DX
2290 YM(NN)=YM(NN)+DY
2300 VPOKE 1,AA,NN
2310 RETURN
2320 REM 
2330 REM  MONSTER ATTACKS (MONSTER IS NN)
2340 REM 
2350 W = INT(RND(1)*10)
2360 ? "THE " M$(NN) " HITS YOU FOR" W "POINTS."
2370 INPUT "PRESS <ENTER>"; YN$
2380 ? "\X91\X91" LI$ :? LI$
2390 PS = PS - W
2400 RETURN
2410 REM 
2420 REM  ATTACK THE MONSTER (MONSTER IS NN)
2430 REM 
2440 T1 = IW AND 7
2450 T2 = (IW/8) AND 7
2460 T3 = (IW/64) AND 7
2470 T4 = (IW/256) AND 7
2480 T5 = T1+T2+T3+T4+4
2490 W = INT(RND(1)*T5*2)
2500 ? "YOU HIT THE " M$(NN) " FOR" W "POINTS";
2510 IF W > RND(1)*NN THEN ? ", KILLING IT";: VPOKE 1,MA(NN),$2E :YM(NN)=-1000
2520 ? "."
2530 INPUT "PRESS <ENTER>"; YN$
2540 ? "\X91\X91" LI$ :? LI$
2550 RETURN
