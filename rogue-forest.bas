5 B1$ = "  ÕÃÃÕÀÉÕÀÉÂ ÂÕÀÀ  ÕÃÃÕÀÉÕÃÃÕÀÀÕÀÃÀ²À "
10 B2$ = "  Â  ÊÀËÊÀÛÊÀË«À   «À ÊÀËÂ  «À ÊÀÉ Â  "
15 B3$ = "         ÀË   ÊÀÀ  Â        ÊÀÀÃÀË0.5 "
20 REM  INITIALIZE DESCRIPTORS
25 DIM M$(31)
30 FOR X=0 TO 31 :READ M$(X) :NEXT
35 DATA  DOPPLEGANGER,  ABALONE,  BASILISK,    CENTAUR,    DRAGON
40 DATA  ETTIN,         FUZZY,    GHOUL,       HELLHOUND,  IMP
45 DATA  JINN,          KOBOLD,   LEPRECHAUN,  MINOTAUR,   NYMPH
50 DATA  ORC,           PYTHON,   QUAGGA,      RATTLER,    SKELETON
55 DATA  TROLL,         UR-VILE,  VAMPIRE,     WRAITH,     XEROC
60 DATA  YETI,          ZOMBIE,   WYVERN,      MANTICORE,  VELOCIRAPTOR
65 DATA  SPHINX,        SPIDER
70 FOR X=0 TO 7 :READ N$(X) :NEXT
75 DATA  DURIN, FOLGAR, SARA, SHARIK, ZOG, CHENEY, REJNALDI, KHAALO
80 FOR X=0 TO 7 :READ C$(X) :NEXT
85 DATA  "",RED,GREEN,BLUE,YELLOW,WHITE,SILVER,GOLDEN
90 FOR X=0 TO 7 :READ D$(X) :NEXT
95 DATA  "",OF WAR,OF AGILITY,OF ENDURING,OF CLEVERNESS,OF SKILL,"",OF MAGIC
100 FOR X=0 TO 7 :READ W$(X) :NEXT
105 DATA DAGGER, QUARTERSTAFF, HAMMER, AXE, SWORD, GREAT AXE, CLAYMORE, HALBERD
110 FOR X=0 TO 7 :READ A$(X) :NEXT
115 DATA LEATHER, STUDDED LEATHER, HAUBERK, RINGMAIL, SCALE MAIL, PLATE MAIL
120 DATA ENCHANTED MAIL, MITHRIL
125 REM  LINE-CLEANER
130 LI$ = "                                                           "
135 REM  SET UP VIDEO REGISTERS
140 POKE $9F2D, %01100000 :REM MAP HEIGHT=1, WIDTH=2 = 64X128 TILES
145 POKE $9F2E, %10000000 :REM MAP BASE ADDR = 128 X512 = $10000.
150 POKE $9F2F, %01111100 :REM TILE BASE ADDR = 31 X2K  = $F800.
155 REM ALSO, TILE HT=0, WD=0, SO 8 PIXELS X 8 PIXELS
160 REM  NO SCROLLING, PLEASE ($9F30-$9F33)
165 POKE $9F30, 0
170 POKE $9F31, 0
175 POKE $9F32, 0
180 POKE $9F33, 0
185 ? CHR$(147)
190 ? B1$ :? B2$ :? B3$  :REM BANNER
195 ? 
200 ? "WELCOME TO ROGUE FOREST"
205 INPUT "PRESS <ENTER> TO BEGIN"; K$
210 X=RND(-TI) :REM INIT
215 REM  BUILD FOREST
220 COLOR 1,0 :CLS  :REM 0=TRANSPARENT
225 FO(0) = $41 :REM TREE
230 FO(1) = $2E :REM DOT
235 FO(2) = $2E :REM DOT
240 FO(3) = $43 :REM HORIZ LINE
245 FO(4) = $42 :REM VERT LINE
250 FO(5) = $25 :REM TREASURE
255 FO(6) = $2A :REM AMULET OF YENDOR
260 POKE $9F29, %00110001 :REM $31=LAYERS 1,0. OUTPUT MODE=1 (VGA)
265 CC=0
270 REM  UNROLLING THIS LOOP SHAVES 5 SECS
275 CL=$05  :REM BG/FG COLOR NYBBLES ($15=WHITE UNDER GREEN)
280 REM MONSTER DATA
285 MN=0
290 DIM MA(31)
295 DIM XM(31),YM(31)
300 DIM SO(31) :REM TERRAIN THE MONSTER IS .S.TANDING .O.N
305 FOR Y=0 TO 59
310    Y0=Y*$100
315    FOR X=0 TO 158 STEP 8
320       P=Y0+X
325       IF Y<7 OR Y>42 GOTO 430
330       IF X<4 OR X>150 GOTO 430
335          FF=3 
340          IF Y>7 AND Y<42 THEN FF=INT(RND(1)*3) 
345          VPOKE 1,P,FO(FF) :REM CHARACTER INDEX
350          VPOKE 1,P+1,CL   :REM BG/FG COLOR NYBBLES
355          IF Y>7 AND Y<42 THEN FF=INT(RND(1)*3) 
360          VPOKE 1,P+2,FO(FF)
365          VPOKE 1,P+3,CL              
370          IF Y>7 AND Y<42 THEN FF=INT(RND(1)*3) 
375          VPOKE 1,P+4,FO(FF)
380          VPOKE 1,P+5,CL              
385          IF Y>7 AND Y<42 THEN FF=INT(RND(1)*3) 
390          VPOKE 1,P+6,FO(FF)
395          VPOKE 1,P+7,CL              
400          IF Y<8 OR Y>41 GOTO 470
405          REM TREASURE AND/OR CRITTER
410          IF RND(1) > 0.06 GOTO 470
415             XX = INT(RND(4))
420             VPOKE 1,P+2*XX,$25 
425             GOTO 470
430             VPOKE 1,P,$20
435             VPOKE 1,P+1,CL
440             VPOKE 1,P+2,$20
445             VPOKE 1,P+3,CL
450             VPOKE 1,P+4,$20
455             VPOKE 1,P+5,CL
460             VPOKE 1,P+6,$20
465             VPOKE 1,P+7,CL
470    NEXT
475 NEXT
480 REM  PLACE MONSTERS
485 FOR M = 0 TO 31
490    X = 10 + INT(RND(1)*60)
495    Y = 10 + INT(RND(1)*30)
500    MA = Y * $100 + X * 2
505    MA(M) = MA
510    XM(M)=X
515    YM(M)=Y
520    SO(M) = VPEEK(1,MA)
525    VPOKE 1,MA,M
530 NEXT
535 REM AMULET OF YENDOR
540 VPOKE 1, INT(RND(1)*30+10) * $100 + INT(RND(1)*60+10) * 2, $2A
545 REM HIDE BACKGROUND
550 COLOR 1,6 :CLS :REM WHITE ON BLUE
555 ? B1$ : ? B2$ : ? B3$
560 REM INITIALIZE DRAWING UTIL
565 DIM VP$(60)
570 VP$(0)="\X13"
575 FOR V=1 TO 60
580    VP$(V) = VP$(V-1) + "\X11"
585 NEXT
590 L5$ = "\X91\X91\X91\X91\X91" :REM LEFT 5
595 REM  INITIALIZE PLAYER
600 X=INT(RND(1)*50+10) :Y=INT(RND(1)*30+10) :REM POSITION
605 SC=0 :HU=0 :REM SCORE, HUNGER LEVEL
610 IW=0 :IA=0 :IT=0  :REM INVENTORIES (WEAPON, ARMOR, TREASURE)
615 PS=INT(RND(1)*50+50)  :REM STR
620 QS=PS
625 PD=INT(RND(1)*50+50)  :REM DEX
630 QD=PD
635 PE=INT(RND(1)*50+50)  :REM END
640 QE=PE
645 PI=INT(RND(1)*50+50)  :REM INT
650 QI=PI
655 REM  CLEAR 9X9 GRID AROUND PLAYER
660 ? VP$(Y-4); :REM LEFT$(PY$,Y);
665 COLOR 1,0 :REM TRANSPARENT (CLEARS FOG)
670 FOR I=1 TO 9
675    ? TAB(X-4) "         "
680 NEXT
685 REM
690 REM PRINT PLAYER AND POSITION CURSOR
695 REM
700 COLOR 1,6
705 ? L5$ TAB(X) "@"
710 ? VP$(43) LI$ : ? "\X91STR" PS "SCORE:" SC
715 ? VP$(46);
720 REM PLAYER MOVEMENT
725 GET A$ :IF A$="" GOTO 725
730 XX=0:YY=0
735 IF A$="\X11" THEN YY=+1 :REM DOWN
740 IF A$="\X91" THEN YY=-1 :REM UP
745 IF A$="\X1D" THEN XX=+1 :REM RIGHT
750 IF A$="\X9D" THEN XX=-1 :REM LEFT
755 LO = VPEEK(1,(Y+YY)*$100 + (X+XX)*2)
760 IF LO < $20 THEN NN=LO :GOSUB 1135 :GOTO 780
765 IF LO < $41 THEN X=X+XX :Y=Y+YY :HU=HU+1
770 IF LO = $25 THEN GOSUB 865
775 IF LO = $2A THEN GOSUB 965
780 FOR NN=0 TO 31
785    AA=MA(NN)
790    XX=XM(NN)-X
795    YY=YM(NN)-Y
800    IF ABS(XX) < 2 AND ABS(YY) < 2 THEN GOSUB 1095 :GOTO 815
805    IF RND(1)>0.5 THEN 815
810    IF ABS(XX) < 9 AND ABS(YY) < 9 THEN GOSUB 1020
815 NEXT
820 IF PS < 0 GOTO 830
825 IF (X<75) AND (X>3) AND (Y<40) AND (Y>5) GOTO 660
830 COLOR 1,$06 :REM WHITE ON BLUE
835 IF PS < 0 AND PY = 0 THEN ? "YOU DIED." :? VP$(Y) TAB(X) CHR$($7B) VP$(46)
840 IF PS < 0 AND PY = 1 THEN ? "YOU ALMOST MADE IT, BUT YOU DIED."
845 IF PS > 0 AND PY = 1 THEN ? "YOU ESCAPED THE FOREST WITH THE AMULET!"
850 IF PS > 0 AND PY = 0 THEN ? "YOU ESCAPED THE FOREST."
855 ? "SCORE=" SC
860 END
865 COLOR 1,6
870 VPOKE 1,Y*$100 + X*2, $2E  :REM CLEAR IT
875 T1=INT(RND(1)*8)
880 T2=INT(RND(1)*8)
885 T3=INT(RND(1)*8)
890 T4=INT(RND(1)*8)
895 SC = SC + T1+T2+T3+T4
900 TT=INT(RND(1)*2)
905 IF TT=0 GOTO 915
910 IF TT=1 GOTO 940
915 ? "YOU FOUND " N$(T1) "'S " C$(T2) " " W$(T3) " " D$(T4)
920 INPUT "DO YOU KEEP IT"; YN$
925 ? "\X91\X91" LI$ :? LI$
930 IF YN$ = "Y" THEN IW = T1 + T2 * 8 + T3 * 64 + T4 * 256
935 RETURN
940 ? "YOU FOUND " N$(T1) "'S " C$(T2) " " A$(T3) " " D$(T4)
945 INPUT "DO YOU KEEP IT"; YN$
950 ? "\X91\X91" LI$ :? LI$
955 IF YN$ = "Y" THEN IA = T1 + T2 * 8 + T3 * 64 + T4 * 256
960 RETURN
965 COLOR 1,6
970 ? "YOU FOUND THE AMULET OF YENDOR! "
975 INPUT "PRESS <ENTER>"; YN$
980 ? "\X91\X91" LI$ :? LI$
985 VPOKE 1,Y*$100 + X*2, $2E  :REM CLEAR IT
990 PY=1  :REM PLAYER-YENDOR
995 RETURN
1000 REM  MOVE MONSTER
1005 REM  - NN IS THE MONSTER NUMBER (0-31)
1010 REM  - ADDRESS IN AA
1015 REM  - RELATIVE POS IN XX,YY
1020 VPOKE 1,AA,SO(NN)  :REM REDRAW TILE
1025 DX=0 :DY=0
1030 IF XX<0 THEN AA=AA+2 :DX=1 :GOTO 1050
1035 IF XX>0 THEN AA=AA-2 :DX=-1 :GOTO 1050
1040 IF YY<0 THEN AA=AA+$100 :DY=1 :GOTO 1050
1045 IF YY>0 THEN AA=AA-$100 :DY=-1 :GOTO 1050
1050 V=VPEEK(1,AA)
1055 IF V < $20 OR V = $2A GOTO 1085
1060 SO(NN)=VPEEK(1,AA)
1065 MA(NN)=AA
1070 XM(NN)=XM(NN)+DX
1075 YM(NN)=YM(NN)+DY
1080 VPOKE 1,AA,NN
1085 RETURN
1090 REM  MONSTER ATTACKS (MONSTER IS NN)
1095 W = INT(RND(1)*10)
1100 PS=PS-W
1105 ? "THE " M$(NN) " HITS YOU FOR" W "POINTS."
1110 INPUT "PRESS <ENTER>"; YN$
1115 ? "\X91\X91" LI$ :? LI$
1120 PS = PS - W
1125 RETURN
1130 REM  ATTACK THE MONSTER (MONSTER IS NN)
1135 T1 = IW AND 7
1140 T2 = (IW/8) AND 7
1145 T3 = (IW/64) AND 7
1150 T4 = (IW/256) AND 7
1155 T5 = T1+T2+T3+T4+4
1160 W = INT(RND(1)*T5*2)
1165 HU=HU+1
1170 ? "YOU HIT THE " M$(NN) " FOR" W "POINTS";
1175 IF W > RND(1)*NN THEN ? ", KILLING IT";: VPOKE 1,MA(NN),$2E :YM(NN)=-1000 :SC=SC+W
1180 ? "."
1185 INPUT "PRESS <ENTER>"; YN$
1190 ? "\X91\X91" LI$ :? LI$
1195 RETURN
