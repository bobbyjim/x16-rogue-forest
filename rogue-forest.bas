10 REM  INITIALIZE DESCRIPTORS
20 DIM M$(31)
30 FOR X=0 TO 31 :READ M$(X) :NEXT
40 DATA  DOPPLEGANGER,  ABALONE,  BASILISK,    CENTAUR,    DRAGON
50 DATA  ETTIN,         FUZZY,    GHOUL,       HELLHOUND,  IMP
60 DATA  JINN,          KOBOLD,   LEPRECHAUN,  MINOTAUR,   NYMPH
70 DATA  ORC,           PYTHON,   QUAGGA,      RATTLER,    SKELETON
80 DATA  TROLL,         UR-VILE,  VAMPIRE,     WRAITH,     XEROC
90 DATA  YETI,          ZOMBIE,   WYVERN,      MANTICORE,  VELOCIRAPTOR
100 DATA  SPHINX,        SPIDER
110 FOR X=0 TO 7 :READ N$(X) :NEXT
120 DATA  DURIN, FOLGAR, SARA, SHARIK, ZOG, CHENEY, REJNALDI, KHAALO
130 FOR X=0 TO 7 :READ C$(X) :NEXT
140 DATA  "",RED,GREEN,BLUE,YELLOW,WHITE,SILVER,GOLDEN
150 FOR X=0 TO 7 :READ D$(X) :NEXT
160 DATA  "",OF WAR,OF AGILITY,OF ENDURING,OF CLEVERNESS,OF SKILL,"",OF MAGIC
170 FOR X=0 TO 7 :READ W$(X) :NEXT
180 DATA DAGGER, QUARTERSTAFF, HAMMER, AXE, SWORD, GREAT AXE, CLAYMORE, HALBERD
190 FOR X=0 TO 7 :READ A$(X) :NEXT
200 DATA LEATHER, STUDDED LEATHER, HAUBERK, RINGMAIL, SCALE MAIL, PLATE MAIL
210 DATA ENCHANTED MAIL, MITHRIL
220 REM  LINE-CLEANER
230 LI$ = "                                                           "
240 REM  SET UP VIDEO REGISTERS
250 POKE $9F2D, %01100000 :REM MAP HEIGHT=1, WIDTH=2 = 64X128 TILES
260 POKE $9F2E, %10000000 :REM MAP BASE ADDR = 128 X512 = $10000.
270 POKE $9F2F, %01111100 :REM TILE BASE ADDR = 31 X2K  = $F800.
280 REM ALSO, TILE HT=0, WD=0, SO 8 PIXELS X 8 PIXELS
290 REM  NO SCROLLING, PLEASE ($9F30-$9F33)
300 POKE $9F30, 0
310 POKE $9F31, 0
320 POKE $9F32, 0
330 POKE $9F33, 0
340 ? CHR$(147) "WELCOME TO ROGUE FOREST"
350 INPUT "PRESS <ENTER> TO BEGIN"; K$
360 X=RND(-TI) :REM INIT
370 REM  BUILD FOREST
380 COLOR 1,0 :CLS  :REM 0=TRANSPARENT
390 FO(0) = $41 :REM TREE
400 FO(1) = $2E :REM DOT
410 FO(2) = $2E :REM DOT
420 FO(3) = $43 :REM HORIZ LINE
430 FO(4) = $42 :REM VERT LINE
440 FO(5) = $25 :REM TREASURE
450 FO(6) = $2A :REM AMULET OF YENDOR
460 POKE $9F29, %00110001 :REM $31=LAYERS 1,0. OUTPUT MODE=1 (VGA)
470 PRINT "\X13\X11\X11\X11\X11COLOR 1,6\X13" 
480 CC=0
490 REM  UNROLLING THIS LOOP SHAVES 5 SECS
500 CL=$05  :REM BG/FG COLOR NYBBLES ($15=WHITE UNDER GREEN)
510 REM MONSTER DATA
520 MN=0
530 DIM MA(31)
540 DIM XM(31),YM(31)
550 DIM SO(31) :REM TERRAIN THE MONSTER IS .S.TANDING .O.N
560 FOR Y=0 TO 45
570    Y0=Y*$100
580    FOR X=0 TO 158 STEP 8
590       P=Y0+X
600       IF Y<5 OR Y>40 GOTO 810
610       IF X<4 OR X>150 GOTO 810
620          FF=3 
630          IF Y>5 AND Y<40 THEN FF=INT(RND(1)*3) 
640          VPOKE 1,P,FO(FF) :REM CHARACTER INDEX
650          VPOKE 1,P+1,CL   :REM BG/FG COLOR NYBBLES
660          IF Y>5 AND Y<40 THEN FF=INT(RND(1)*3) 
670          VPOKE 1,P+2,FO(FF)
680          VPOKE 1,P+3,CL              
690          IF Y>5 AND Y<40 THEN FF=INT(RND(1)*3) 
700          VPOKE 1,P+4,FO(FF)
710          VPOKE 1,P+5,CL              
720          IF Y>5 AND Y<40 THEN FF=INT(RND(1)*3) 
730          VPOKE 1,P+6,FO(FF)
740          VPOKE 1,P+7,CL              
750          IF Y<6 OR Y>39 GOTO 890
760          REM TREASURE AND/OR CRITTER
770          IF RND(1) > 0.06 GOTO 890
780             XX = INT(RND(4))
790             VPOKE 1,P+2*XX,$25 
800             GOTO 890
810             VPOKE 1,P,$20
820             VPOKE 1,P+1,CL
830             VPOKE 1,P+2,$20
840             VPOKE 1,P+3,CL
850             VPOKE 1,P+4,$20
860             VPOKE 1,P+5,CL
870             VPOKE 1,P+6,$20
880             VPOKE 1,P+7,CL
890    NEXT
900 NEXT
910 REM  PLACE MONSTERS
920 FOR M = 0 TO 31
930    X = 10 + INT(RND(1)*60)
940    Y = 10 + INT(RND(1)*20)
950    MA = Y * $100 + X * 2
960    MA(M) = MA
970    XM(M)=X
980    YM(M)=Y
990    SO(M) = VPEEK(1,MA)
1000    VPOKE 1,MA,M
1010 NEXT
1020 REM AMULET OF YENDOR
1030 VPOKE 1, INT(RND(1)*25+10) * $100 + INT(RND(1)*50+20) * 2, $2A
1040 REM HIDE BACKGROUND
1050 COLOR 1,6 :CLS :REM WHITE ON BLUE
1060 REM INITIALIZE DRAWING UTIL
1070 DIM VP$(60)
1080 VP$(0)="\X13"
1090 FOR V=1 TO 60
1100    VP$(V) = VP$(V-1) + "\X11"
1110 NEXT
1120 L5$ = "\X91\X91\X91\X91\X91" :REM LEFT 5
1130 REM  INITIALIZE PLAYER
1140 X=10 :Y=10 :REM POSITION
1150 SC=0 :HU=0 :REM SCORE, HUNGER LEVEL
1160 IW=0 :IA=0 :IT=0  :REM INVENTORIES (WEAPON, ARMOR, TREASURE)
1170 PS=INT(RND(1)*50+50)  :REM STR
1180 QS=PS
1190 PD=INT(RND(1)*50+50)  :REM DEX
1200 QD=PD
1210 PE=INT(RND(1)*50+50)  :REM END
1220 QE=PE
1230 PI=INT(RND(1)*50+50)  :REM INT
1240 QI=PI
1250 REM  CLEAR 9X9 GRID AROUND PLAYER
1260 ? VP$(Y-4); :REM LEFT$(PY$,Y);
1270 COLOR 1,0 :REM TRANSPARENT (CLEARS FOG)
1280 FOR I=1 TO 9
1290    ? TAB(X-4) "         "
1300 NEXT
1310 REM
1320 REM PRINT PLAYER AND POSITION CURSOR
1330 REM
1340 COLOR 1,6
1350 ? L5$ TAB(X) "@"
1360 ? VP$(43) PS "/" QS, PD "/" QD, PE "/" QE, PI "/" QI, SC, HU
1370 ? VP$(46);
1380 REM PLAYER MOVEMENT
1390 GET A$ :IF A$="" GOTO 1390
1400 XX=0:YY=0
1410 IF A$="\X11" THEN YY=+1 :REM DOWN
1420 IF A$="\X91" THEN YY=-1 :REM UP
1430 IF A$="\X1D" THEN XX=+1 :REM RIGHT
1440 IF A$="\X9D" THEN XX=-1 :REM LEFT
1450 LO = VPEEK(1,(Y+YY)*$100 + (X+XX)*2)
1460 IF LO < $20 THEN NN=LO :GOSUB 2210 :GOTO 1500
1470 IF LO < $41 THEN X=X+XX :Y=Y+YY :HU=HU+1
1480 IF LO = $25 THEN GOSUB 1670
1490 IF LO = $2A THEN GOSUB 1870
1500 FOR NN=0 TO 31
1510    AA=MA(NN)
1520    XX=XM(NN)-X
1530    YY=YM(NN)-Y
1540    IF ABS(XX) < 2 AND ABS(YY) < 2 THEN GOSUB 2130 :GOTO 1570
1550    IF RND(1)>0.5 THEN 1570
1560    IF ABS(XX) < 9 AND ABS(YY) < 9 THEN GOSUB 1980
1570 NEXT
1580 IF PS < 0 GOTO 1600
1590 IF (X<75) AND (X>3) AND (Y<40) AND (Y>5) GOTO 1260
1600 COLOR 1,$06 :REM WHITE ON BLUE
1610 IF PS < 0 AND PY = 0 THEN ? "YOU DIED." :? VP$(Y) TAB(X) CHR$($7B) VP$(46)
1620 IF PS < 0 AND PY = 1 THEN ? "YOU ALMOST MADE IT, BUT YOU DIED."
1630 IF PS > 0 AND PY = 1 THEN ? "YOU ESCAPED THE FOREST WITH THE AMULET!"
1640 IF PS > 0 AND PY = 0 THEN ? "YOU ESCAPED THE FOREST."
1650 ? "SCORE=" SC
1660 END
1670 COLOR 1,6
1680 VPOKE 1,Y*$100 + X*2, $2E  :REM CLEAR IT
1690 T1=INT(RND(1)*8)
1700 T2=INT(RND(1)*8)
1710 T3=INT(RND(1)*8)
1720 T4=INT(RND(1)*8)
1730 SC = SC + T1+T2+T3+T4
1740 TT=INT(RND(1)*2)
1750 IF TT=0 GOTO 1770
1760 IF TT=1 GOTO 1820
1770 ? "YOU FOUND " N$(T1) "'S " C$(T2) " " W$(T3) " " D$(T4)
1780 INPUT "DO YOU KEEP IT"; YN$
1790 ? "\X91\X91" LI$ :? LI$
1800 IF YN$ = "Y" THEN IW = T1 + T2 * 8 + T3 * 64 + T4 * 256
1810 RETURN
1820 ? "YOU FOUND " N$(T1) "'S " C$(T2) " " A$(T3) " " D$(T4)
1830 INPUT "DO YOU KEEP IT"; YN$
1840 ? "\X91\X91" LI$ :? LI$
1850 IF YN$ = "Y" THEN IA = T1 + T2 * 8 + T3 * 64 + T4 * 256
1860 RETURN
1870 COLOR 1,6
1880 ? "YOU FOUND THE AMULET OF YENDOR! "
1890 INPUT "PRESS <ENTER>"; YN$
1900 ? "\X91\X91" LI$ :? LI$
1910 VPOKE 1,Y*$100 + X*2, $2E  :REM CLEAR IT
1920 PY=1  :REM PLAYER-YENDOR
1930 RETURN
1940 REM  MOVE MONSTER
1950 REM  - NN IS THE MONSTER NUMBER (0-31)
1960 REM  - ADDRESS IN AA
1970 REM  - RELATIVE POS IN XX,YY
1980 VPOKE 1,AA,SO(NN)  :REM REDRAW TILE
1990 DX=0 :DY=0
2000 IF XX<0 THEN AA=AA+2 :DX=1 :GOTO 2040
2010 IF XX>0 THEN AA=AA-2 :DX=-1 :GOTO 2040
2020 IF YY<0 THEN AA=AA+$100 :DY=1 :GOTO 2040
2030 IF YY>0 THEN AA=AA-$100 :DY=-1 :GOTO 2040
2040 V=VPEEK(1,AA)
2050 IF V < $20 OR V = $2A GOTO 2110
2060 SO(NN)=VPEEK(1,AA)
2070 MA(NN)=AA
2080 XM(NN)=XM(NN)+DX
2090 YM(NN)=YM(NN)+DY
2100 VPOKE 1,AA,NN
2110 RETURN
2120 REM  MONSTER ATTACKS (MONSTER IS NN)
2130 W = INT(RND(1)*10)
2140 PS=PS-W
2150 ? "THE " M$(NN) " HITS YOU FOR" W "POINTS."
2160 INPUT "PRESS <ENTER>"; YN$
2170 ? "\X91\X91" LI$ :? LI$
2180 PS = PS - W
2190 RETURN
2200 REM  ATTACK THE MONSTER (MONSTER IS NN)
2210 T1 = IW AND 7
2220 T2 = (IW/8) AND 7
2230 T3 = (IW/64) AND 7
2240 T4 = (IW/256) AND 7
2250 T5 = T1+T2+T3+T4+4
2260 W = INT(RND(1)*T5*2)
2270 HU=HU+1
2280 ? "YOU HIT THE " M$(NN) " FOR" W "POINTS";
2290 IF W > RND(1)*NN THEN ? ", KILLING IT";: VPOKE 1,MA(NN),$2E :YM(NN)=-1000 :SC=SC+W
2300 ? "."
2310 INPUT "PRESS <ENTER>"; YN$
2320 ? "\X91\X91" LI$ :? LI$
2330 RETURN
