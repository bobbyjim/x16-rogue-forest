5 REM
10 REM "ÕÃÃÕÀÉÕÀÉÂ ÂÕÀÀ  ÕÃÃÕÀÉÕÃÃÕÀÀÕÀÃÀ²À "
15 REM "Â  ÊÀËÊÀÛÊÀË«À   «À ÊÀËÂ  «À ÊÀÉ Â  "
20 REM "       ÀË   ÊÀÀ  Â        ÊÀÀÃÀË    "
25 REM
30 REM  INITIALIZE PLAYER
35 GOSUB 920  :REM INIT GAME
40 GOSUB 1705  :REM INIT SOUND
45 GOSUB 1300 :REM INIT PANELS
50 REM  LINE-CLEANER
55 LI$ = "                                                           "
60 GOSUB 1735 :REM SPLASH SCREEN
65 GOSUB 460  :REM BUILD FOREST
70 R1=INT(RND(1)*50+10)
75 R2=INT(RND(1)*30+10)
80 R3  = 0
85 R4 = 0
90 R5   = 1
95 R6 = 1
100 R7  = 0
105 R6$ = "RUSTY SPORK"
110 R7$ = "TUXEDO"
115 C1    = INT(RND(1)*6)+INT(RND(1)*6)+2 :REM STR
120 C2  = C1
125 C3    = INT(RND(1)*6)+INT(RND(1)*6)+2 :REM DEX
130 C4    = INT(RND(1)*6)+INT(RND(1)*6)+2 :REM END
135 C5    = INT(RND(1)*6)+INT(RND(1)*6)+2 :REM INT
140 C0(1) = C1
145 C0(2) = C3
150 C0(3) = C4
155 C0(4) = C5
160 R8$ = "NOT YET"
165 DIM IW%(10), IA%(10) :REM WEAPONS, ARMOR
170 REM                       DRAW THE SCREEN
175 REM HIDE BACKGROUND
180 COLOR 1,6 :CLS :REM WHITE ON BLUE
185 GOSUB 1905 :REM TITLE
190 GOSUB 1665 
195 L= 1  :T= 44 :W= 76  :H= 15  :L$= "" :GOSUB 1615
200 GOSUB 1475 :REM DRAW PANELS
205 GOSUB 380
210 REM  PRINT PLAYER AND POSITION CURSOR
215 ? VP$(R2-4);
220 COLOR 1,0 :REM TRANSPARENT (CLEARS FOG)
225 ? TAB(R1-4) C9$;
230 COLOR 1,6 :REM BLUE UNDER WHITE
235 ? L5$ TAB(R1) "@"
240 ? VP$(4) LI$ : ? "\X91   STR" C1 "SCORE:" R3 "HUNGER:" R4
245 IF RND(1)>0.95 THEN R4 = R4 + 1
250 T0 = T0 - 1
255 IF T0 = 0 THEN ? VP$(5) LI$
260 GET A$ :IF A$="" GOTO 260
265 XX=0 :YY=0
270 IF A$="\X11" THEN YY=+1 :REM DOWN
275 IF A$="\X91" THEN YY=-1 :REM UP
280 IF A$="\X1D" THEN XX=+1 :REM RIGHT
285 IF A$="\X9D" THEN XX=-1 :REM LEFT
290 IF A$="E" THEN GOSUB 415
295 IF A$="." AND C1 < C2 THEN C1 = C1 + 1
300 LO = VPEEK(1,(R2+YY)*$100 + (R1+XX)*2)
305 IF LO < $20 THEN NN=LO :GOSUB 1230 :GOTO 320
310 IF LO > $22 AND LO < $2E THEN :GOSUB 1930 :GOSUB 380
315 IF LO < $41 THEN R1=R1+XX :R2=R2+YY :HU=HU+1: LO = VPEEK(1,R2*$100 + R1*2)
320 FOR NN=0 TO 31
325    AA=MA(NN)
330    XX=XM(NN)-R1
335    YY=YM(NN)-R2
340    IF ABS(XX)+ABS(YY) <= 1 THEN GOSUB 1190 :GOTO 355
345    IF RND(1)>0.5 THEN 355
350    IF ABS(XX) < 9 AND ABS(YY) < 9 THEN GOSUB 1110
355 NEXT
360 IF C1 < 1 GOTO 370
365 IF (R1<76) AND (R1>3) AND (R2<42) AND (R2>5) GOTO 215
370 GOSUB 830
375 END
380 ? VP$(46);
385 ? SPC(6) LI$: ? "\X91" SPC(6) "WEAPON: " R6$ " (" R6 ")"
390 ? SPC(6) LI$: ? "\X91" SPC(6) "ARMOR : " R7$ " (" R7 ")"
395 ? SPC(6) LI$: ? "\X91" SPC(6) "FOOD  : " R5
400 ? SPC(6) LI$: ? "\X91" SPC(6) "AMULET: " R8$ 
405 ?
410 RETURN
415 IF R5 < 1 THEN RETURN
420 IF R4 < 1 THEN RETURN
425 R5 = R5 - 1
430 R4 = R4 - 1
435 T0 = 5
440 ? VP$(5) LI$ :?"\X91   YOU FEEL BETTER."
445 GOSUB 380
450 RETURN
455 REM                         BUILD THE FOREST
460 REM  SET UP VIDEO REGISTERS
465 POKE $9F2D, %01100000 :REM MAP HEIGHT=1, WIDTH=2 = 64X128 TILES
470 POKE $9F2E, %10000000 :REM MAP BASE ADDR = 128 X512 = $10000.
475 POKE $9F2F, %01111100 :REM TILE BASE ADDR = 31 X2K  = $F800.
480 REM ALSO, TILE HT=0, WD=0, SO 8 PIXELS X 8 PIXELS
485 REM  NO SCROLLING, PLEASE ($9F30-$9F33)
490 POKE $9F30, 0
495 POKE $9F31, 0
500 POKE $9F32, 0
505 POKE $9F33, 0
510 COLOR 1,0 :CLS  :REM 0=TRANSPARENT
515 POKE $9F29, %00110001 :REM $31=LAYERS 1,0. OUTPUT MODE=1 (VGA)
520 CC=0:CD=59 
525 IF VPEEK(1,0) = 32 THEN CC=8 :CD=40
530 REM  UNROLLING THIS LOOP SHAVES 5 SECS
535 CL=$05  :REM BG/FG COLOR NYBBLES ($15=WHITE UNDER GREEN)
540 REM MONSTER DATA
545 MN=0
550 DIM MA(31)
555 DIM XM(31),YM(31)
560 DIM SO(31) :REM TERRAIN THE MONSTER IS .S.TANDING .O.N
565 FOR Y=CC TO CD
570    Y0=Y*$100
575    FOR X=0 TO 158 STEP 8
580       P=Y0+X
585       IF Y<8 OR Y>40 GOTO 690
590       IF X<6 OR X>148 GOTO 690
595          FF=3
600          IF Y>8 AND Y<40 THEN FF=INT(RND(1)*3)
605          VPOKE 1,P,FO(FF) :REM CHARACTER INDEX
610          VPOKE 1,P+1,CL   :REM BG/FG COLOR NYBBLES
615          IF Y>8 AND Y<40 THEN FF=INT(RND(1)*3)
620          VPOKE 1,P+2,FO(FF)
625          VPOKE 1,P+3,CL
630          IF Y>8 AND Y<40 THEN FF=INT(RND(1)*3)
635          VPOKE 1,P+4,FO(FF)
640          VPOKE 1,P+5,CL
645          IF Y>8 AND Y<40 THEN FF=INT(RND(1)*3)
650          VPOKE 1,P+6,FO(FF)
655          VPOKE 1,P+7,CL
660          IF Y<9 OR Y>39 GOTO 730
665          REM TREASURE
670          IF RND(1) > 0.08 GOTO 730
675             XX = INT(RND(4))
680             VPOKE 1,P+2*XX,FO(5+INT(RND(1)*4))
685             GOTO 730
690             VPOKE 1,P,$20
695             VPOKE 1,P+1,CL
700             VPOKE 1,P+2,$20
705             VPOKE 1,P+3,CL
710             VPOKE 1,P+4,$20
715             VPOKE 1,P+5,CL
720             VPOKE 1,P+6,$20
725             VPOKE 1,P+7,CL
730    NEXT
735 NEXT
740 REM  PLACE MONSTERS
745 FOR M = 1 TO 26 :REM 0 TO 31
750    X = 10 + INT(RND(1)*60)
755    Y = 10 + INT(RND(1)*30)
760    XX = ABS(X-R1)
765    YY = ABS(Y-R2)
770    IF XX<6 AND YY<6 GOTO 750 :REM TOO CLOSE!
775    MA = Y * $100 + X * 2
780    MA(M) = MA
785    XM(M)=X
790    YM(M)=Y
795    SO(M) = VPEEK(1,MA)
800    VPOKE 1,MA,M
805 NEXT
810 REM AMULET OF YENDOR
815 VPOKE 1, INT(RND(1)*30+10) * $100 + INT(RND(1)*60+10) * 2, FO(10)
820 RETURN
825 REM                      GAME OVER - PRINT SCORE
830 COLOR 1,$06 :REM WHITE ON BLUE
835 ? CHR$(147) :REM CLR
840 GOSUB 1665
845 L= 1  :T= 1  :W= 76  :H= 17  :L$= "GAME OVER"   :GOSUB 1615
850 GOSUB 1475
855 ? VP$(3)
860 ?SPC(6) "YOUR ACCOMPLISHMENTS"
865 ?:?:? SPC(6) "ESCAPED THE FOREST: ";
870 IF C1 < 1 THEN ? "NO"
875 IF C1 > 0 THEN ? "YES"
880 ?:? SPC(6) "FOUND THE AMULET:   ";
885 IF R8 = 0 THEN ? "NO"
890 IF R8 = 1 THEN ? "YES"
895 ?:?:? SPC(6) "SCORE:             " R3
900 ?:?:? SPC(6) "THANK YOU FOR PLAYING ROGUE FOREST!"
905 ?:?:?
910 RETURN
915 REM  INITIALIZE 
920 GOSUB 2230 :REM INIT VP$()
925 L5$ = "\X91\X91\X91\X91\X91" :REM LEFT 5
930 C9$ = "         \X11\X9D\X9D\X9D\X9D\X9D\X9D\X9D\X9D\X9D"
935 C9$ = C9$+C9$+C9$+C9$+C9$+C9$+C9$+C9$+C9$
940 DIM M$(31)
945 FOR X=0 TO 31 :READ M$(X) :NEXT
950 DATA  DOPPLEGANGER,  ABALONE,  BASILISK,    CENTAUR,    DRAGON
955 DATA  ETTIN,         FUZZY,    GHOUL,       HELLHOUND,  IMP
960 DATA  JINN,          KOBOLD,   LEPRECHAUN,  MINOTAUR,   NYMPH
965 DATA  ORC,           PYTHON,   QUAGGA,      RATTLER,    SKELETON
970 DATA  TROLL,         UR-VILE,  VAMPIRE,     WRAITH,     XEROC
975 DATA  YETI,          ZOMBIE,   WYVERN,      MANTICORE,  VELOCIRAPTOR
980 DATA  SPHINX,        SPIDER
985 FOR X=0 TO 7 :READ N$(X) :NEXT
990 DATA CHENEY, DURIN, FARGOL, KHAALO, REJNALDI, SARA, SHARIK, ZOG
995 FOR X=0 TO 7 :READ C$(X) :NEXT
1000 DATA "","RED ","GREEN ","BLUE ","TIN ","WHITE ","PALE ","GOLD "
1005 FOR X=0 TO 7 :READ D$(X) :NEXT
1010 DATA ""," OF WAR"," OF AGILITY"," OF PAIN"," OF CUNNING"
1015 DATA " OF SKILL",""," OF MAGIC"
1020 FOR X=0 TO 7 :READ W$(X) :NEXT
1025 DATA DAGGER, STAFF, HAMMER, MACHETE, AXE, SWORD, LABRYS, HALBERD
1030 FOR X=0 TO 7 :READ A$(X) :NEXT
1035 DATA LEATHER, HAUBERK, RINGMAIL, MESH, MAIL, SPLINT, PLATE, MITHRIL
1040 REM FOREST ELEMENTS
1045 FO(0) = $41 :REM TREE
1050 FO(1) = $2E :REM DOT
1055 FO(2) = $2E :REM DOT
1060 FO(3) = $43 :REM HORIZ LINE
1065 FO(4) = $42 :REM VERT LINE
1070 FO(5) = $23 :REM ARMOR (#)
1075 FO(6) = $24 :REM FOOD ($)
1080 FO(7) = $27 :REM WEAPON (')
1085 FO(8) = $27
1090 FO(9) = $24
1095 FO(10) = $2A :REM AMULET OF YENDOR
1100 RETURN
1105 REM        MOVE MONSTER:NN ADDRESS:AA RELATIVE_POS:XX,YY
1110 VPOKE 1,AA,SO(NN)  :REM REDRAW TILE
1115 DX=0 :DY=0
1120 IF RND(1)>0.5 GOTO 1135
1125 IF XX<0 THEN AA=AA+2 :DX=1 :GOTO 1145
1130 IF XX>0 THEN AA=AA-2 :DX=-1 :GOTO 1145
1135 IF YY<0 THEN AA=AA+$100 :DY=1 :GOTO 1145
1140 IF YY>0 THEN AA=AA-$100 :DY=-1 :GOTO 1145
1145 V=VPEEK(1,AA)
1150 IF V < $20 OR V = $2A GOTO 1180
1155 SO(NN)=VPEEK(1,AA)
1160 MA(NN)=AA
1165 XM(NN)=XM(NN)+DX
1170 YM(NN)=YM(NN)+DY
1175 VPOKE 1,AA,NN
1180 RETURN
1185 REM                  MONSTER NN ATTACKS
1190 ? VP$(51) SPC(6) LI$ : ? "\X91";
1195 W = INT(RND(1)*4)
1200 IF INT(RND(1)*40) < R7 THEN W=0
1205 IF W=0 THEN ? SPC(6) "THE " M$(NN) " MISSES."
1210 IF W>0 THEN ? SPC(6) "THE " M$(NN) " HITS YOU FOR" W "POINTS." :GOSUB 1715
1215 C1 = C1 - W
1220 RETURN
1225 REM                  ATTACK MONSTER NN
1230 ? VP$(52) SPC(6) LI$ : ? "\X91";
1235 T1 = R6 AND 7
1240 T2 = (R6/8) AND 7
1245 T3 = (R6/64) AND 7
1250 T4 = (R6/256) AND 7
1255 T5 = T1+T2+T3+T4+1 - R4
1260 IF T5 < 2 THEN T5 = 2
1265 W = INT(RND(1)*T5)
1270 HU=HU+1
1275 N=RND(1) * NN
1280 ? SPC(6) "YOU HIT THE " M$(NN) " FOR" W "POINTS"; :GOSUB 1715
1285 IF W > N THEN ? ", KILLING IT";: VPOKE 1,MA(NN),$2E :YM(NN)=-1000 :R3=R3+W
1290 ? "."
1295 RETURN
1300 REM SET UP PANEL ARRAYS BASED ON MAX
1305 REM NUMBER OF PANELS (N=6)
1310 N = 6
1315 P7 = 0
1320 DIM P0$(N)
1325 DIM P1(N), P2(N), P3(N), P4(N)
1330 DIM P5$(N,30), P5SELECTED(N)
1335 GOSUB 2230 :REM INIT VP$()
1340 REM HORIZONTAL BAR ARRAY
1345 DIM HB$(80)
1350 HB$(0) = "\XC3"
1355 FOR V=1 TO 80
1360    HB$(V) = HB$(V-1) + "\XC3"
1365 NEXT
1370 REM VERTICAL BAR ARRAY
1375 DIM VB$(30)
1380 VB$(0) = "\XC2\X11\X9D"
1385 FOR V=1 TO 30
1390    VB$(V) = VB$(V-1) + "\XC2\X11\X9D"
1395 NEXT
1400 REM  PANEL EDGES
1405 BA$ = "\X9D\X9D\X9D\X9D\X9D\X9D\X9D\X9D\X9D\X9D"
1410 BA$ = BA$ + "\X9D\X9D\X9D\X9D\X9D\X9D\X9D\X9D\X9D\X9D"
1415 BA$ = BA$ + "\X9D\X9D\X9D\X9D\X9D\X9D\X9D\X9D\X9D\X9D"
1420 BA$ = BA$ + "\X9D\X9D\X9D\X9D\X9D\X9D\X9D\X9D\X9D\X9D"
1425 BA$ = BA$ + "\X9D\X9D\X9D\X9D\X9D\X9D\X9D\X9D\X9D\X9D"
1430 BA$ = BA$ + "\X9D\X9D\X9D\X9D\X9D\X9D\X9D\X9D\X9D\X9D"
1435 BA$ = BA$ + "\X9D\X9D\X9D\X9D\X9D\X9D\X9D\X9D\X9D\X9D"
1440 BA$ = BA$ + "\X9D\X9D\X9D\X9D\X9D\X9D\X9D\X9D\X9D\X9D"
1445 SP$ = "                    "
1450 SP$ = SP$ + "                    "
1455 SP$ = SP$ + "                    "
1460 SP$ = SP$ + "                    "
1465 RETURN
1470 REM DRAW PANELS FROM VARIABLE DATA
1475 FOR N = 0 TO P7 - 1
1480    ? VP$(P1(N)) SPC(P2(N))
1485    ? "\XD5\XC3" P0$(N) "\XC3";
1490    ? HB$(P3(N) - 3 - LEN(P0$(N)));
1495    ? "\XC9\X11";
1500    B2$ = LEFT$(BA$, P3(N)+2)
1505    S2$ = LEFT$(SP$, P3(N))
1510    FOR RR=3 TO P4(N)
1515    REM DON'T FORGET THE LINES !!
1520    ? B2$ "\XC2" S2$ "\XC2\X11";
1525    NEXT
1530    ? B2$ "\XCA";
1535    ? HB$(P3(N)-1);
1540    ? "\XCB";
1545 NEXT
1550 RETURN
1555 REM SHOW THE PANEL TEXTS
1560 FOR N = 0 TO P7 - 1
1565    ? VP$(P1(N));
1570    ?
1575    FOR X = 0 TO P4(N)-3
1580       ? SPC(P2(N)+2) P5$(N,X)
1585    NEXT
1590 NEXT
1595 RETURN
1600 REM ADD A PANEL TO THE LIST!
1605 REM T: TOP, L: LEFT, W: WIDTH, H:HEIGHT
1610 REM L$: LABEL
1615 N = P7
1620 P0$(N) = L$
1625 P1(N) = T
1630 P2(N) = L
1635 P3(N) = W
1640 P4(N) = H
1645 P5SELECTED(N) = -1
1650 P7 = N + 1
1655 RETURN
1660 REM "DELETE" ALL PANELS
1665 P7 = 0
1670 RETURN
1675 REM SET CONTENTS OF A PANEL
1680 REM N:PANEL NUM, BU$(): LINES, LC: LINE COUNT
1685 FOR X = 0 TO LC-1
1690    P5$(N,X) = BU$(X)
1695 NEXT
1700 RETURN
1705 RETURN
1710 RETURN
1715 RETURN
1720 RETURN
1725 RETURN
1730 REM                       SPLASH SCREEN!
1735 ? CHR$(147)
1740 L= 1  :T= 5  :W= 76  :H= 13  :L$= "WELCOME"     :GOSUB 1615
1745 L= 1  :T= 19 :W= 76  :H= 15  :L$= "MAP KEY"     :GOSUB 1615
1750 L= 1  :T= 35 :W= 76  :H= 12  :L$= "COMMAND KEY" :GOSUB 1615
1755 GOSUB 1475 :REM DRAW PANELS
1760 ? VP$(0); :GOSUB 1905 :REM TITLE
1765 ? VP$(8)
1770 ? SPC(6) "FIND THE AMULET OF YENDOR AND EXIT THE FOREST ALIVE!"
1775 ?
1780 ? SPC(6) "POINTS ARE AWARDED FOR ITEMS COLLECTED AND MONSTERS KILLED."
1785 ? VP$(20)
1790 ? SPC(6) "@ ... YOU"
1795 ? SPC(6) CHR$(FO(0)) " ... TREE (IMPASSABLE BY YOU)"
1800 ? SPC(6) CHR$(FO(1)) " ... CLEARING"
1805 ? SPC(6) CHR$(FO(5)) " ... ARMOR"
1810 ? SPC(6) CHR$(FO(6)) " ... FOOD"
1815 ? SPC(6) CHR$(FO(7)) " ... WEAPON"
1820 ? SPC(6) CHR$(FO(10))" ... AMULET OF YENDOR"
1825 ?:?
1830 ? SPC(6) "-- ANYTHING THAT MOVES IS A MONSTER --"
1835 ? VP$(36)
1840 ? SPC(6) "? ............ HELP"
1845 ? SPC(6) "CURSOR KEYS .. MOVE / ATTACK"
1850 ? SPC(6) ". ............ REST"
1855 ? SPC(6) "E ............ EAT FOOD"
1860 ? SPC(6) "X ............ EXIT"
1865 ?
1870 ? VP$(50)
1875 ? "   GOOD LUCK!"
1880 ?
1885 INPUT "   PLEASE TYPE YOUR NAME"; R0$
1890 X=RND(-TI)
1895 RETURN
1900 REM PRINT TITLE
1905 ? "  ÕÃÃÕÀÉÕÀÉÂ ÂÕÀÀ  ÕÃÃÕÀÉÕÃÃÕÀÀÕÀÃÀ²À "
1910 ? "  Â  ÊÀËÊÀÛÊÀË«À   «À ÊÀËÂ  «À ÊÀÉ Â  "
1915 ? "         ÀË   ÊÀÀ  Â        ÊÀÀÃÀË1.0 "
1920 RETURN
1925 REM                   TREASURE FINDS (TYPE IN 'LO')
1930 COLOR 1,6
1935 REM CLEAR THE SQUARE THE PLAYER IS *LOOKING AT*
1940 VPOKE 1,(R2+YY)*$100 + (R1+XX)*2, $2E
1945 GOSUB 1710
1950 REM PRE-CALC SOME VALUES
1955 T1=INT(RND(1)*8)
1960 T2=INT(RND(1)*8)
1965 T3=INT(RND(1)*8)
1970 T4=INT(RND(1)*8)
1975 REM FIGURE OUT WHAT WE GOT
1980 IF LO=FO(10) GOTO 2005
1985 IF LO=FO(5)  GOTO 2055
1990 IF LO=FO(6)  GOTO 2030
1995 IF LO=FO(7)  GOTO 2110
2000 RETURN
2005 ? VP$(5) LI$ :? "\X91   YOU FOUND THE AMULET OF YENDOR! "
2010 R8$ = "FOUND IT!"
2015 R8=1  :REM PLAYER-YENDOR
2020 T0 = 5
2025 RETURN
2030 T1 = INT(RND(1)*8+1)
2035 ? VP$(5) LI$ :?"\X91   YOU FOUND" T1 "FOOD."
2040 R5=R5 + T1
2045 T0 = 5
2050 RETURN
2055 R3 = R3 + T1+T2+T3+T4
2060 AD$ = N$(T1) + "'S " + C$(T2) + A$(T3) + D$(T4)
2065 GOSUB 2165
2070 ? SPC(6) "YOU FOUND " AD$
2075 ?:? SPC(6) "REPLACE YOUR CURRENT ARMOR WITH IT? [YN] "; 
2080 GET YN$ :IF YN$<> "Y" AND YN$ <> "N" GOTO 2080
2085 GOSUB 2195
2090 IF YN$ = "N" THEN RETURN
2095 R7 = T1 + T2 +T3 + T4 
2100 R7$ = AD$
2105 RETURN
2110 R3 = R3 + T1+T2+T3+T4
2115 WD$ = N$(T1) + "'S " + C$(T2) + W$(T3) + D$(T4)
2120 GOSUB 2165
2125 ? SPC(6) "YOU FOUND " WD$
2130 ?:? SPC(6) "REPLACE YOUR CURRENT WEAPON WITH IT? [YN] "; 
2135 GET YN$ :IF YN$<> "Y" AND YN$ <> "N" GOTO 2135
2140 GOSUB 2195
2145 IF YN$ = "N" THEN RETURN
2150 R6 = T1 + T2 * 8 + T3 * 64 + T4 * 256
2155 R6$ = WD$
2160 RETURN
2165 ? VP$(51);
2170 ?SPC(6) LI$
2175 ?SPC(6) LI$
2180 ?SPC(6) LI$
2185 ?"\X91\X91\X91";
2190 RETURN
2195 ? "\X91\X91\X91\X91"
2200 ?SPC(6) LI$ 
2205 ?SPC(6) LI$ 
2210 ?SPC(6) LI$ 
2215 ?SPC(6) LI$
2220 RETURN
2225 REM INITIALIZE VERTICAL POSITION ARRAY
2230 IF VP=1 THEN RETURN
2235 DIM VP$(60)
2240 VP$(0)="\X13"
2245 FOR V=1 TO 60
2250    VP$(V) = VP$(V-1) + "\X11"
2255 NEXT
2260 VP=1
2265 RETURN
