5 REM
10 REM ÕÃÃÕÀÉÕÀÉÂ ÂÕÀÀ  ÕÃÃÕÀÉÕÃÃÕÀÀÕÀÃÀ²À 
15 REM Â  ÊÀËÊÀÛÊÀË«À   «À ÊÀËÂ  «À ÊÀÉ Â 
20 REM        ÀË   ÊÀÀ  Â        ÊÀÀÃÀË
25 REM
30 REM  INITIALIZE PLAYER
35 GOSUB 825 :REM INIT GAME
40 GOSUB 1225 :REM INIT SOUND
45 REM  LINE-CLEANER
50 LI$ = "                                                           "
55 GOSUB 1285 :REM SPLASH SCREEN
60 GOSUB 475  :REM BUILD FOREST
65 P1=INT(RND(1)*50+10)
70 P2=INT(RND(1)*30+10)
75 P3  = 0
80 P4 = 0
85 P5   = 1
90 P6 = 1
95 P7  = 0
100 P6$ = "RUSTY SPORK"
105 P7$ = "TUXEDO"
110 C1    = INT(RND(1)*6)+INT(RND(1)*6)+2 :REM STR
115 C2  = C1
120 C3    = INT(RND(1)*6)+INT(RND(1)*6)+2 :REM DEX
125 C4    = INT(RND(1)*6)+INT(RND(1)*6)+2 :REM END
130 C5    = INT(RND(1)*6)+INT(RND(1)*6)+2 :REM INT
135 C0(1) = C1
140 C0(2) = C3
145 C0(3) = C4
150 C0(4) = C5
155 P8$ = "NOT YET"
160 DIM IW%(10), IA%(10) :REM WEAPONS, ARMOR
165 REM                       DRAW THE SCREEN
170 REM HIDE BACKGROUND
175 COLOR 1,6 :CLS :REM WHITE ON BLUE
180 GOSUB 1420 :REM TITLE
185 GOSUB 400
190 REM  PRINT PLAYER AND POSITION CURSOR
195 ? VP$(P2-4);
200 COLOR 1,0 :REM TRANSPARENT (CLEARS FOG)
205 ? TAB(P1-4) C9$;
210 COLOR 1,6 :REM BLUE UNDER WHITE
215 ? L5$ TAB(P1) "@"
220 ? VP$(4) LI$ : ? "\X91   STR" C1 "SCORE:" P3 "HUNGER:" P4
225 IF RND(1)>0.95 THEN P4 = P4 + 1
230 T0 = T0 - 1
235 IF T0 = 0 THEN ? VP$(5) LI$
240 GET A$ :IF A$="" GOTO 240
245 XX=0 :YY=0
250 IF A$="\X11" THEN YY=+1 :REM DOWN
255 IF A$="\X91" THEN YY=-1 :REM UP
260 IF A$="\X1D" THEN XX=+1 :REM RIGHT
265 IF A$="\X9D" THEN XX=-1 :REM LEFT
270 IF A$="E" THEN GOSUB 430
275 IF A$="." AND C1 < C2 THEN C1 = C1 + 1
280 LO = VPEEK(1,(P2+YY)*$100 + (P1+XX)*2)
285 IF LO < $20 THEN NN=LO :GOSUB 1155 :GOTO 300
290 IF LO > $22 AND LO < $2E THEN :GOSUB 1445 :GOSUB 400
295 IF LO < $41 THEN P1=P1+XX :P2=P2+YY :HU=HU+1: LO = VPEEK(1,P2*$100 + P1*2)
300 FOR NN=0 TO 31
305    AA=MA(NN)
310    XX=XM(NN)-P1
315    YY=YM(NN)-P2
320    IF ABS(XX)+ABS(YY) <= 1 THEN GOSUB 1115 :GOTO 335
325    IF RND(1)>0.5 THEN 335
330    IF ABS(XX) < 9 AND ABS(YY) < 9 THEN GOSUB 1035
335 NEXT
340 IF C1 < 0 GOTO 355
345 IF (P1<76) AND (P1>3) AND (P2<42) AND (P2>5) GOTO 195
350 REM                      GAME OVER - PRINT SCORE
355 COLOR 1,$06 :REM WHITE ON BLUE
360 ? CHR$(147) :REM CLR
365 ? VP$(4); 
370 IF C1 < 1 AND P8 = 0 THEN ? "YOU DIED." :? VP$(P2) TAB(P1) CHR$($7B) VP$(50)
375 IF C1 < 1 AND P8 = 1 THEN ? "YOU ALMOST MADE IT, BUT YOU DIED."
380 IF C1 > 0 AND P8 = 1 THEN ? "YOU ESCAPED THE FOREST WITH THE AMULET!"
385 IF C1 > 0 AND P8 = 0 THEN ? "YOU ESCAPED THE FOREST."
390 ? "SCORE=" P3
395 END
400 ? VP$(45);
405 ? SPC(20) LI$: ? "\X91" SPC(30) "WEAPON: " P6$
410 ? SPC(20) LI$: ? "\X91" SPC(30) "ARMOR : " P7$
415 ? SPC(20) LI$: ? "\X91" SPC(30) "FOOD  : " P5
420 ? SPC(20) LI$: ? "\X91" SPC(30) "AMULET: " P8$ 
425 RETURN
430 IF P5 < 1 THEN RETURN
435 IF P4 < 1 THEN RETURN
440 P5 = P5 - 1
445 P4 = P4 - 1
450 T0 = 5
455 ? VP$(5) LI$ :?"\X91   YOU FEEL BETTER."
460 GOSUB 400
465 RETURN
470 REM                         BUILD THE FOREST
475 REM  SET UP VIDEO REGISTERS
480 POKE $9F2D, %01100000 :REM MAP HEIGHT=1, WIDTH=2 = 64X128 TILES
485 POKE $9F2E, %10000000 :REM MAP BASE ADDR = 128 X512 = $10000.
490 POKE $9F2F, %01111100 :REM TILE BASE ADDR = 31 X2K  = $F800.
495 REM ALSO, TILE HT=0, WD=0, SO 8 PIXELS X 8 PIXELS
500 REM  NO SCROLLING, PLEASE ($9F30-$9F33)
505 POKE $9F30, 0
510 POKE $9F31, 0
515 POKE $9F32, 0
520 POKE $9F33, 0
525 COLOR 1,0 :CLS  :REM 0=TRANSPARENT
530 POKE $9F29, %00110001 :REM $31=LAYERS 1,0. OUTPUT MODE=1 (VGA)
535 CC=0
540 REM  UNROLLING THIS LOOP SHAVES 5 SECS
545 CL=$05  :REM BG/FG COLOR NYBBLES ($15=WHITE UNDER GREEN)
550 REM MONSTER DATA
555 MN=0
560 DIM MA(31)
565 DIM XM(31),YM(31)
570 DIM SO(31) :REM TERRAIN THE MONSTER IS .S.TANDING .O.N
575 FOR Y=0 TO 59
580    Y0=Y*$100
585    FOR X=0 TO 158 STEP 8
590       P=Y0+X
595       IF Y<8 OR Y>40 GOTO 700
600       IF X<6 OR X>148 GOTO 700
605          FF=3
610          IF Y>8 AND Y<40 THEN FF=INT(RND(1)*3)
615          VPOKE 1,P,FO(FF) :REM CHARACTER INDEX
620          VPOKE 1,P+1,CL   :REM BG/FG COLOR NYBBLES
625          IF Y>8 AND Y<40 THEN FF=INT(RND(1)*3)
630          VPOKE 1,P+2,FO(FF)
635          VPOKE 1,P+3,CL
640          IF Y>8 AND Y<40 THEN FF=INT(RND(1)*3)
645          VPOKE 1,P+4,FO(FF)
650          VPOKE 1,P+5,CL
655          IF Y>8 AND Y<40 THEN FF=INT(RND(1)*3)
660          VPOKE 1,P+6,FO(FF)
665          VPOKE 1,P+7,CL
670          IF Y<9 OR Y>39 GOTO 740
675          REM TREASURE
680          IF RND(1) > 0.08 GOTO 740
685             XX = INT(RND(4))
690             VPOKE 1,P+2*XX,FO(5+INT(RND(1)*4))
695             GOTO 740
700             VPOKE 1,P,$20
705             VPOKE 1,P+1,CL
710             VPOKE 1,P+2,$20
715             VPOKE 1,P+3,CL
720             VPOKE 1,P+4,$20
725             VPOKE 1,P+5,CL
730             VPOKE 1,P+6,$20
735             VPOKE 1,P+7,CL
740    NEXT
745 NEXT
750 REM  PLACE MONSTERS
755 FOR M = 0 TO 31
760    X = 10 + INT(RND(1)*60)
765    Y = 10 + INT(RND(1)*30)
770    MA = Y * $100 + X * 2
775    MA(M) = MA
780    XM(M)=X
785    YM(M)=Y
790    SO(M) = VPEEK(1,MA)
795    VPOKE 1,MA,M
800 NEXT
805 REM AMULET OF YENDOR
810 VPOKE 1, INT(RND(1)*30+10) * $100 + INT(RND(1)*60+10) * 2, FO(10)
815 RETURN
820 REM  INITIALIZE 
825 REM INITIALIZE DRAWING UTIL
830 DIM VP$(60)
835 VP$(0)="\X13"
840 FOR V=1 TO 60
845    VP$(V) = VP$(V-1) + "\X11"
850 NEXT
855 L5$ = "\X91\X91\X91\X91\X91" :REM LEFT 5
860 C9$ = "         \X11\X9D\X9D\X9D\X9D\X9D\X9D\X9D\X9D\X9D"
865 C9$ = C9$+C9$+C9$+C9$+C9$+C9$+C9$+C9$+C9$
870 DIM M$(31)
875 FOR X=0 TO 31 :READ M$(X) :NEXT
880 DATA  DOPPLEGANGER,  ABALONE,  BASILISK,    CENTAUR,    DRAGON
885 DATA  ETTIN,         FUZZY,    GHOUL,       HELLHOUND,  IMP
890 DATA  JINN,          KOBOLD,   LEPRECHAUN,  MINOTAUR,   NYMPH
895 DATA  ORC,           PYTHON,   QUAGGA,      RATTLER,    SKELETON
900 DATA  TROLL,         UR-VILE,  VAMPIRE,     WRAITH,     XEROC
905 DATA  YETI,          ZOMBIE,   WYVERN,      MANTICORE,  VELOCIRAPTOR
910 DATA  SPHINX,        SPIDER
915 FOR X=0 TO 7 :READ N$(X) :NEXT
920 DATA CHENEY, DURIN, FARGOL, KHAALO, REJNALDI, SARA, SHARIK, ZOG
925 FOR X=0 TO 7 :READ C$(X) :NEXT
930 DATA "","RED ","GREEN ","BLUE ","YELLOW ","WHITE ","SILVER ","GOLDEN "
935 FOR X=0 TO 7 :READ D$(X) :NEXT
940 DATA ""," OF WAR"," OF AGILITY"," OF ENDURING"," OF CUNNING"
945 DATA " OF SKILL",""," OF MAGIC"
950 FOR X=0 TO 7 :READ W$(X) :NEXT
955 DATA DAGGER, STAFF, HAMMER, AXE, SWORD, GREAT AXE, CLAYMORE, HALBERD
960 FOR X=0 TO 7 :READ A$(X) :NEXT
965 DATA LEATHER, HAUBERK, RINGMAIL, CHAIN MAIL, SPLINT, PLATE MAIL
970 DATA ENCHANTED MAIL, MITHRIL
975 REM FOREST ELEMENTS
980 FO(0) = $41 :REM TREE
985 FO(1) = $2E :REM DOT
990 FO(2) = $2E :REM DOT
995 FO(3) = $43 :REM HORIZ LINE
1000 FO(4) = $42 :REM VERT LINE
1005 FO(5) = $23 :REM ARMOR (#)
1010 FO(6) = $24 :REM FOOD ($)
1015 FO(7) = $27 :REM WEAPON (')
1020 FO(10) = $2A :REM AMULET OF YENDOR
1025 RETURN
1030 REM        MOVE MONSTER:NN ADDRESS:AA RELATIVE_POS:XX,YY
1035 VPOKE 1,AA,SO(NN)  :REM REDRAW TILE
1040 DX=0 :DY=0
1045 IF RND(1)>0.5 GOTO 1060
1050 IF XX<0 THEN AA=AA+2 :DX=1 :GOTO 1070
1055 IF XX>0 THEN AA=AA-2 :DX=-1 :GOTO 1070
1060 IF YY<0 THEN AA=AA+$100 :DY=1 :GOTO 1070
1065 IF YY>0 THEN AA=AA-$100 :DY=-1 :GOTO 1070
1070 V=VPEEK(1,AA)
1075 IF V < $20 OR V = $2A GOTO 1105
1080 SO(NN)=VPEEK(1,AA)
1085 MA(NN)=AA
1090 XM(NN)=XM(NN)+DX
1095 YM(NN)=YM(NN)+DY
1100 VPOKE 1,AA,NN
1105 RETURN
1110 REM                  MONSTER NN ATTACKS
1115 ? VP$(51) LI$ : ? "\X91";
1120 W = INT(RND(1)*4)
1125 IF INT(RND(1)*40) < P7 THEN W=0
1130 IF W=0 THEN ? "THE " M$(NN) " MISSES."
1135 IF W>0 THEN ? "THE " M$(NN) " HITS YOU FOR" W "POINTS." :GOSUB 1265
1140 C1 = C1 - W
1145 RETURN
1150 REM                  ATTACK MONSTER NN
1155 ? VP$(52) LI$ : ? "\X91";
1160 T1 = P6 AND 7
1165 T2 = (P6/8) AND 7
1170 T3 = (P6/64) AND 7
1175 T4 = (P6/256) AND 7
1180 T5 = T1+T2+T3+T4+1 - P4
1185 IF T5 < 2 THEN T5 = 2
1190 W = INT(RND(1)*T5)
1195 HU=HU+1
1200 N=RND(1) * NN
1205 ? "YOU HIT THE " M$(NN) " FOR" W "POINTS"; :GOSUB 1245
1210 IF W > N THEN ? ", KILLING IT";: VPOKE 1,MA(NN),$2E :YM(NN)=-1000 :P3=P3+W
1215 ? "."
1220 RETURN
1225 IF PEEK($400)=0 THEN LOAD "RF-EFFECTS.PRG",8,1,$0400
1230 RETURN
1235 SYS $0400
1240 RETURN
1245 SYS $0403
1250 RETURN
1255 SYS $0406
1260 RETURN
1265 SYS $0409
1270 FOR T=1 TO 1000 :NEXT
1275 RETURN
1280 REM                       SPLASH SCREEN!
1285 ? CHR$(147)
1290 GOSUB 1420 :REM TITLE
1295 ?
1300 ? "WELCOME TO ROGUE FOREST"
1305 ?
1310 ? "MAP KEY"
1315 ? "   @ ... YOU"
1320 ? "   " CHR$(FO(0)) " ... TREE (IMPASSABLE BY YOU)"
1325 ? "   " CHR$(FO(1)) " ... CLEARING"
1330 ? "   " CHR$(FO(5)) " ... ARMOR"
1335 ? "   " CHR$(FO(6)) " ... FOOD"
1340 ? "   " CHR$(FO(8)) " ... WEAPON"
1345 ? "   " CHR$(FO(10))" ... AMULET OF YENDOR"
1350 ?
1355 ? "    ANYTHING THAT MOVES IS A MONSTER"
1360 ?
1365 ? "COMMAND KEY"
1370 ? "   ? ............ HELP"
1375 ? "   CURSOR KEYS .. MOVE/ATTACK"
1380 ? "   . ............ REST"
1385 ? "   E ............ EAT FOOD"
1390 ? "   X ............ EXIT"
1395 ?
1400 INPUT "PLEASE TYPE YOUR NAME"; P0$
1405 X=RND(-TI)
1410 RETURN
1415 REM PRINT TITLE
1420 ? "  ÕÃÃÕÀÉÕÀÉÂ ÂÕÀÀ  ÕÃÃÕÀÉÕÃÃÕÀÀÕÀÃÀ²À "
1425 ? "  Â  ÊÀËÊÀÛÊÀË«À   «À ÊÀËÂ  «À ÊÀÉ Â  "
1430 ? "         ÀË   ÊÀÀ  Â        ÊÀÀÃÀË0.8 "
1435 RETURN
1440 REM                   TREASURE FINDS (TYPE IN 'LO')
1445 COLOR 1,6
1450 REM CLEAR THE SQUARE THE PLAYER IS *LOOKING AT*
1455 VPOKE 1,(P2+YY)*$100 + (P1+XX)*2, $2E
1460 GOSUB 1235
1465 REM PRE-CALC SOME VALUES
1470 T1=INT(RND(1)*8)
1475 T2=INT(RND(1)*8)
1480 T3=INT(RND(1)*8)
1485 T4=INT(RND(1)*8)
1490 REM FIGURE OUT WHAT WE GOT
1495 IF LO=FO(10) GOTO 1520
1500 IF LO=FO(5)  GOTO 1545
1505 IF LO=FO(6)  GOTO 1600
1510 IF LO=FO(7)  GOTO 1625
1515 RETURN
1520 ? VP$(5) LI$ :? "\X91   YOU FOUND THE AMULET OF YENDOR! "
1525 P8$ = "FOUND IT!"
1530 P8=1  :REM PLAYER-YENDOR
1535 T0 = 5
1540 RETURN
1545 P3 = P3 + T1+T2+T3+T4
1550 AD$ = N$(T1) + "'S " + C$(T2) + A$(T3) + D$(T4)
1555 GOSUB 1680
1560 ? "YOU FOUND " AD$
1565 ? "REPLACE YOUR CURRENT ARMOR WITH IT? [YN] "; 
1570 GET YN$ :IF YN$<> "Y" AND YN$ <> "N" GOTO 1570
1575 GOSUB 1690
1580 IF YN$ = "N" THEN RETURN
1585 P7 = T1 + T2 * 8 + T3 * 64 + T4 * 256
1590 P7$ = AD$
1595 RETURN
1600 T1 = INT(RND(1)*8+1)
1605 ? VP$(5) LI$ :?"\X91   YOU FOUND" T1 "FOOD."
1610 P5=P5 + T1
1615 T0 = 5
1620 RETURN
1625 P3 = P3 + T1+T2+T3+T4
1630 WD$ = N$(T1) + "'S " + C$(T2) + W$(T3) + D$(T4)
1635 GOSUB 1680
1640 ? "YOU FOUND " WD$
1645 ? "REPLACE YOUR CURRENT WEAPON WITH IT? [YN] "; 
1650 GET YN$ :IF YN$<> "Y" AND YN$ <> "N" GOTO 1650
1655 GOSUB 1690
1660 IF YN$ = "N" THEN RETURN
1665 P6 = T1 + T2 * 8 + T3 * 64 + T4 * 256
1670 P6$ = WD$
1675 RETURN
1680 ? VP$(51) LI$ :?"\X91";
1685 RETURN
1690 ? "\X91\X91\X91" LI$ :?LI$ :?LI$
1695 RETURN
